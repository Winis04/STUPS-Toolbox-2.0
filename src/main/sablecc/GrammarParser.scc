Package GrammarParser;

Helpers
    letter = ['a'..'z'] | ['A'..'Z'];
    digit = ['0'..'9'];
    tab = 9;
    lf = 10;
    cr = 13;
    
Tokens
    whitespace =  (' ' | tab | cr | lf)+;
    arrow = '-->';
    l_bracket = '(';
    r_bracket = ')';
    l_bracket2 = '{';
    r_bracket2 = '}';
    semicolon = ';';
    comma = ',';
    line = '|';
    identifier = ('_' | letter)('_'  | letter | digit)*;
    symbol = ''' [[0..0xffff] - ''']+ ''';

Ignored Tokens
    whitespace;

Productions
    s {-> root} = {grammar} symbols rule_list {-> New root(symbols, [rule_list.rule])};
    symbols {-> symbols} = {symbols} l_bracket2 [terminals]: symbol_list [first_semi]: semicolon [nonterminals]: identifierlist [last_semi]: semicolon [start_symbol]: identifier r_bracket2 {-> New symbols([terminals.symbol], [nonterminals.identifier], start_symbol)};
    identifierlist {-> identifier*} = {single_id} identifier {-> [identifier]} | {mult_id} identifier comma identifierlist {-> [identifier, identifierlist.identifier]};
    symbol_list {-> symbol*} = {single_symbol} symbol {-> [symbol]} | {mult_symbol} symbol comma symbol_list {-> [symbol, symbol_list.symbol]};
    rule_list {-> rule*} = {single_rule} rule {-> [rule.rule]} | {mult_rule} rule rule_list {-> [rule, rule_list.rule]};
    rule {-> rule} = {rule} [coming_from]: identifier arrow [going_to]: symbol_list {-> New rule(coming_from, [going_to.symbol])};
    
Abstract Syntax Tree
    root = symbols rule*;
    symbols = [terminals]: symbol* [nonterminals]: identifier* [start_symbol]: identifier;
    rule = [coming_from]: identifier [going_to]: symbol*;
